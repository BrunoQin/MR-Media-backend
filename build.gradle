buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath ('org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        xml.destination = "${buildDir}/jacocoRootReport.xml"
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/jacocoRootReport.xml"
}

war {
    baseName = 'MR_Media'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    providedRuntime
    ebeanAgent
    compile.exclude module: 'slf4j-log4j12'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile('org.avaje.ebeanorm:avaje-ebeanorm:6.18.1')
    compile('org.apache.httpcomponents:httpclient')
    compile('com.google.code.gson:gson:2.3.1')
    compile('org.json:json')
    compile('ch.qos.logback:logback-classic:1.1.7')
    compile('ch.qos.logback:logback-core:1.1.7')
    compile("org.springframework.boot:spring-boot-devtools")
    compile ("io.springfox:springfox-swagger2:2.6.1")
    compile ('com.jayway.jsonpath:json-path:2.2.0')
    compile ('io.springfox:springfox-swagger-ui:2.6.1')
    ebeanAgent('org.avaje.ebeanorm:avaje-ebeanorm-agent:4.9.1')
    runtime('mysql:mysql-connector-java')
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

ant.taskdef(
        name: "ebeanEnhance",
        classname:"com.avaje.ebean.enhance.ant.AntEnhanceTask",
        classpath:configurations.ebeanAgent.asPath

)

compileJava.doLast {
    ant.ebeanEnhance(classSource: destinationDir,
            packages: "com.mr.media.model.*",
            transformArgs: "debug=1")
}
